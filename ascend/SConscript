#!/usr/bin/env python
Import('env')

if env.get('HAVE_ASCEND'):

	libenv = env.Clone()

	# ascend flags, as retrieved from the ascend-config script
	libenv.Append(
		LIBS=env.get('ASCEND_LIBS')
		,LIBPATH=env.get('ASCEND_LIBPATH')
		,CPPPATH=env.get('ASCEND_CPPPATH')
	)

	# link to the freesteam library too
	libenv.Append(
		LIBS=['freesteam']
		,LIBPATH=['#']
	)

	# build the library
	lib = libenv.SharedLibrary("freesteam",["freesteam_ascend.c"]
		,SHLIBSUFFIX = env['ASCEND_EXTLIB_SUFFIX']
		,SHLIBPREFIX = env['ASCEND_EXTLIB_PREFIX']		
	)

	env.Depends(lib,env['libfreesteam'])

	env['installedfiles'] += [env.InstallLibrary("${INSTALL_ROOT}$ASCEND_MODELS",[lib])]
	env['installedfiles'] += [env.Install("${INSTALL_ROOT}$ASCEND_MODELS/freesteam",env.Glob("*.a4c"))]

# vim: set syntax=python:
