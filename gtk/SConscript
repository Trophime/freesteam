#!/usr/bin/env python
Import('env')

if env.get('HAVE_GTK'):

	env['GTK_GLADE_DIR'] = "$INSTALL_SHARE/freesteam"
	env['GTK_GLADE_FILE'] = "freesteam-gtk.glade"

	gtk_env = env.Clone()

	gtk_env.Append(
		LIBPATH=["#"]+env.get('GTK_LIBPATH')
		,LIBS=['freesteam'] + env.get('GTK_LIBS')
		,CPPPATH=['#',env.get('GTK_CPPPATH')]
		,LINKFLAGS=env.get('GTK_LINKFLAGS')
		,CPPDEFINES={
			"GTK_GLADE_DIR":"\\\"${GTK_GLADE_DIR}\\\""
			,"GTK_GLADE_FILE":"\\\"${GTK_GLADE_FILE}\\\""		
			,'FREESTEAM_LOCAL':1
		}
	)

	prog = gtk_env.Program("freesteam-gtk",['freesteam-gtk.c','callbacks.c'])

	env['installedfiles'] += [env.Install("${INSTALL_ROOT}${GTK_GLADE_DIR}",["${GTK_GLADE_FILE}"])]
	env['installedfiles'] += [env.Install("${INSTALL_ROOT}${GTK_GLADE_DIR}",["icon.png"])]
	env['installedfiles'] += [env.Install("${INSTALL_ROOT}$INSTALL_BIN",[prog])]

